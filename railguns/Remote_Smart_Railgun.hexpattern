#include "../macros/Linear_Impulse.hexpattern"
#include "../macros/Armor_Compensator.hexpattern"
#include "../macros/Flow_Control.hexpattern"

{
    // get target
    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification

    Dioscuri Gambit
    Additive Distillation
    Rotation Gambit II
    Scout's Distillation

    // get living entity at target, make sure it's not me or null
    // this ensures we're shooting something that's worth shooting
    Compass' Purification
    Entity Purification: Living
    Gemini Decomposition
    Nullary Reflection
    Equality Distillation

    Prospector's Gambit
    Mind's Reflection
    Equality Distillation
    Disjunction Distillation
    Conditional Charon

    // get all non-living entities currently where the arrow will show up
    Jester's Gambit
    Numerical Reflection: 1
    Dioscuri Gambit
    Zone Distillation: Non-Living
    Rotation Gambit II
    
    // summon the arrow from the holding chamber
    Prospector's Gambit
    {
        Bookkeeper's Gambit: -- // gate iota placeholder
    }
    Flock's Disintegration
    Jester's Gambit
    Gate's Closing
    
    // select the arrow
    // should be the only entity in this zone and not the other
    // but only select the first one in case multiple arrows got marked somehow
    Zone Distillation: Non-Living
    Exclusionary Distillation
    Numerical Reflection: 0
    Selection Distillation

    // get the arrow -> target vectors
    Jester's Gambit
    Dioscuri Gambit
    Compass' Purification
    Jester's Gambit
    Compass' Purification II
    Subtractive Distillation
    
    Gemini Decomposition
    Length Purification
    Division Distillation
    
    // get the speed to kill the target
    Jester's Gambit
    Gemini Decomposition // optional: compensate for armor
    Nurse's Purification
    Numerical Reflection: 2 // with Power I: 3, II: 3.5, III: 4, IV: 4.5, V: 5
    Division Distillation
    Armor Compensator // optional: compensate for armor
    Multiplicative Distillation

    // cancel existing arrow velocity and shoot it real good
    Prospector's Gambit
    Pace Purification
    Subtractive Distillation
    Linear Impulse
}

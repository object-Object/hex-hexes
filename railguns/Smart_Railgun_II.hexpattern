#include "../macros/Linear_Impulse.hexpattern"
#include "../macros/Armor_Compensator.hexpattern"
#include "../macros/Flow_Control.hexpattern"

// template: Stuck Arrow Filter

// replacement for Thoth's Gambit
// show number of remaining usable arrows nearby
{
    Numerical Reflection: -1
    Huginn's Gambit
    Thoth's Gambit
    Muninn's Reflection
    Reveal
    Bookkeeper's Gambit: v
}

// replacement for Reveal
{
    // increment arrow count
    Muninn's Reflection
    Numerical Reflection: 1
    Additive Distillation
    Gemini Decomposition
    Huginn's Gambit

    // continue if we've already shot an arrow
    Numerical Reflection: 0
    Inequality Distillation
    Conditional Continue

    // get targeted entity
    // could use sentinel walking here
    Mind's Reflection
    Compass' Purification
    Mind's Reflection
    Alidade's Purification
    Scout's Distillation

    // make sure target isn't null
    Gemini Decomposition
    Nullary Reflection
    Equality Distillation
    Conditional Continue

    // get living entity at target, make sure it's not me or null
    Compass' Purification
    Entity Purification: Living
    Gemini Decomposition
    Nullary Reflection
    Equality Distillation

    Prospector's Gambit
    Mind's Reflection
    Equality Distillation
    Disjunction Distillation
    Conditional Continue

    // shoot it real good
    Dioscuri Gambit
    Compass' Purification
    Jester's Gambit
    Compass' Purification II
    Subtractive Distillation
    
    Gemini Decomposition
    Length Purification
    Division Distillation
    
    Jester's Gambit
    Gemini Decomposition // optional: compensate for armor
    Nurse's Purification
    Numerical Reflection: 2 // with Power I: 3, II: 3.5, III: 4, IV: 4.5, V: 5
    Division Distillation
    Armor Compensator // optional: compensate for armor
    Multiplicative Distillation

    Prospector's Gambit
    Numerical Reflection: 0
    Prospector's Gambit
    Popularity Reflection
    Augur's Purification
    {
        Send Iota
        Bookkeeper's Gambit: vv
    }
    Flock's Disintegration
    Augur's Exaltation
    Hermes' Gambit

    Pace Purification
    Subtractive Distillation
    Linear Impulse
}

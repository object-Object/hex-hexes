#include "./types/hexical.hexpattern"
#include "./types/hexcasting.hexpattern"

{
    Consideration: {
        // item magnet gate
        {
            <entity anchored gate>
        }
        Flock's Disintegration

        // targeted block
        Mind's Reflection
        Compass' Purification
        Mind's Reflection
        Alidade's Purification
        Archer's Distillation

        // if not holding telepathy, do an item magnet around the target instead
        Telepathic Reflection
        Numerical Reflection: -1
        Maximus Distillation
        Vacant Reflection
        {
            // if no block targeted, center magnet on self
            Gemini Decomposition
            Nullary Reflection
            Inequality Distillation
            Jester's Gambit
            Mind's Reflection
            Compass' Purification
            Augur's Exaltation

            // mark all items/xp near the target position
            {
                <type: experience orb>
                Dioscuri Gambit
                Marked Distillation
                {
                    Bookkeeper's Gambit: vv
                    Gate's Opening
                }
                Flock's Disintegration
                Augur's Exaltation
                Hermes' Gambit
            }
            Speaker's Decomposition
            Rotation Gambit
            Numerical Reflection: 8 // magnet radius

            Numerical Reflection: 3
            Dioscuri Gambit II
            Zone Exaltation: Type

            Rotation Gambit II
            Zone Distillation: Item
            Additive Distillation
            Bookkeeper's Gambit: v-

            Thoth's Gambit
            Bookkeeper's Gambit: v
            
            // close the gate if anything was marked
            Gemini Decomposition
            Length Purification
            Numerical Reflection: 0
            Maximus Distillation
            {
                Gate's Closing
            }
            Vacant Reflection
            Augur's Exaltation
            Hermes' Gambit

            Janus' Gambit
        }
        Augur's Exaltation
        Hermes' Gambit

        // target type
        Gemini Decomposition
        Sorter's Purification

        // don't break budding amethyst
        {
            <type: budding amethyst>
        }
        Flock's Disintegration
        Prospector's Gambit
        Equality Distillation
        {
            Janus' Gambit
        }
        Vacant Reflection
        Augur's Exaltation
        Hermes' Gambit

        // break first block
        Prospector's Gambit
        Gemini Decomposition
        Break Block
        
        // mark items/xp to be slurped
        {
            <type: experience orb>
            Bookkeeper's Gambit: vv-
            Gate's Opening
        }
        Speaker's Decomposition
        Rotation Gambit
        Numerical Reflection: 0.75

        Numerical Reflection: 3
        Dioscuri Gambit II
        Zone Exaltation: Type

        Rotation Gambit II
        Zone Distillation: Item
        Additive Distillation
        Bookkeeper's Gambit: v-

        Thoth's Gambit

        // increment broken count
        Soroban Reflection
        Bookkeeper's Gambit: vv

        // main loop
        Consideration: {
            // get adjacent blocks
            Consideration: {
                // pos + offset
                Additive Distillation
                
                // check ambit
                Gemini Decomposition
                Mind's Reflection
                Compass' Purification
                Subtractive Distillation
                Length Purification
                Numerical Reflection: 32
                Minimus Distillation
                
                Vacant Reflection
                {
                    <Sekhmet's Gambit: 0>
                    Atalanta's Gambit
                }
                Augur's Exaltation
                Hermes' Gambit

                // check type
                Undertaker's Gambit
                Sorter's Purification
                Equality Distillation
                
                Vacant Reflection
                {
                    <Sekhmet's Gambit: 0>
                    Atalanta's Gambit
                }
                Augur's Exaltation
                Hermes' Gambit

                // increment broken count, and stop if reached limit
                Soroban Reflection
                Numerical Reflection: 64
                Minimus Distillation
                
                Vacant Reflection
                {
                    Janus' Gambit
                }
                Augur's Exaltation
                Hermes' Gambit

                // break block
                Gemini Decomposition
                Break Block
                
                // mark items/xp to be slurped
                {
                    Bookkeeper's Gambit: v-
                    Dioscuri Gambit
                    Marked Distillation
                    {
                        Bookkeeper's Gambit: vv
                        Gate's Opening
                    }
                    Flock's Disintegration
                    Augur's Exaltation
                    Hermes' Gambit
                }
                Speaker's Decomposition
                Numerical Reflection: 2
                Fisherman's Gambit II
                Numerical Reflection: 0.75

                Numerical Reflection: 3
                Dioscuri Gambit II
                Zone Exaltation: Type

                Rotation Gambit II
                Zone Distillation: Item
                Additive Distillation
                Bookkeeper's Gambit: v-

                Thoth's Gambit
                Bookkeeper's Gambit: v-v
            Consideration: }
            {
                // adjacent offsets
                <[(-1, -1, -1), ..., (1, 1, 1)]>
            }
            Flock's Disintegration
            Thoth's Gambit

            // enqueue filtered adjacent blocks
            {
                Speaker's Decomposition
                Gemini Decomposition
                Nullary Reflection
                Inequality Distillation
                {
                    Pollux's Gambit
                    Charon's Gambit
                }
                Flock's Disintegration
                Augur's Exaltation
                Hermes' Gambit
            }
            Sisyphus' Gambit
            <Sekhmet's Gambit: 0> // not strictly necessary, but we don't need the thoth accumulator for anything
        Consideration: }
        Rotation Gambit
        Single's Purification
        Thoth's Gambit
    Consideration: }

    // cast it in a cassette to remove particles, which apparently cause lots of lag too
    Numerical Reflection: 1
    Enqueue
}

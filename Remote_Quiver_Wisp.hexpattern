// macros
#define Conditional Charon (SOUTH_WEST aqdeed) = bool ->

// template: Permanent_Wisp

// replacement for Numerical Reflection: 10
// because you can easily use up ~7 media if you're firing while it's trying to refuel
Numerical Reflection: 25

// replacement for Reveal
{
    // zone distillation in the arrow chamber
    // this is probably in a reasonably controlled environment
    // so don't need to bother with the full stuck arrow detector
    Gemini Decomposition
    Compass' Purification
    Vector Reflection +Y
    Numerical Reflection: 3
    Multiplicative Distillation
    Additive Distillation
    Numerical Reflection: 2
    
    Dioscuri Gambit
    Zone Distillation: Non-Item
    Rotation Gambit II
    Zone Distillation: Non-Living
    Intersection Distillation
    
    // check if there's an arrow in the chamber
    // TODO: allow multiple arrows?
    // TODO: mark if unmarked, not if flag is true
    Gemini Decomposition
    Augur's Purification
    {
        // if there is an arrow, abort if we've already marked it
        Muninn's Reflection
        Augur's Purification
        Negation Purification
        Conditional Charon
        
        // mark the arrow
        Speaker's Decomposition
        {
            Bookkeeper's Gambit: -- // gate iota placeholder
        }
        Flock's Disintegration
        Jester's Gambit
        Gate's Opening
        
        // forget that we dispensed an arrow
        False Reflection
        Huginn's Gambit
    }
    {
        // if there isn't an arrow, abort if we've already dispensed one
        Muninn's Reflection
        Augur's Purification
        Conditional Charon
        
        // dispense an arrow
        Prospector's Gambit
        Compass' Purification
        Vector Reflection +Y
        Numerical Reflection: 7
        Multiplicative Distillation
        Additive Distillation
        
        Gemini Decomposition
        Conjure Block
        Break Block

        // remember that we dispensed an arrow
        True Reflection
        Huginn's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
    Bookkeeper's Gambit: v
}

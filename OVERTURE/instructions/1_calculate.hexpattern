// v-
{
    // select R1 and R2
    Muninn's Reflection
    Undertaker's Gambit
    Numerical Reflection: 1
    Selection Distillation

    Numerical Reflection: 1
    Numerical Reflection: 3
    Selection Exaltation
    Flock's Disintegration
    Rotation Gambit

    // sub-instructions
    // number, number -> number
    { // or
        Unifying Distillation
    }
    { // nand
        Intersection Distillation
        Inversion Purification
    }
    { // nor
        Unifying Distillation
        Inversion Purification
    }
    { // and
        Intersection Distillation
    }
    { // add
        Additive Distillation
        Numerical Reflection: 255
        Intersection Distillation
    }
    { // sub
        Subtractive Distillation
        Numerical Reflection: 255
        Intersection Distillation
    }

    // select and execute sub-instruction
    Numerical Reflection: 6
    Flock's Gambit
    Jester's Gambit
    Selection Distillation
    Hermes' Gambit

    // put result into R3
    Jester's Gambit
    Flock's Disintegration
    
    Rotation Gambit
    Numerical Reflection: 3
    Jester's Gambit
    Surgeon's Exaltation
    
    Numerical Reflection: 2
    Flock's Gambit
    Huginn's Gambit
}

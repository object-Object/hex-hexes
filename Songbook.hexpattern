#define Create Songbook = -> songbook
/// An early-game spellbook in a cassette.
{
    // create new storage props
    Schrödinger's Reflection
    Schrödinger's Reflection
    Dioscuri Gambit

    // start with 16 empty pages
    Nullary Reflection
    Numerical Reflection: 16
    Gemini Decomposition
    Numerical Reflection: 16
    Flock's Gambit

    Undertaker's Gambit
    Schrödinger's Gambit
    Schrödinger's Gambit

    // songbook
    Consideration: {
        // pages prop
        {
            Bookkeeper's Gambit: - // property: pages
        }
        Flock's Disintegration

        // cassette
        Consideration: {
            {
                Bookkeeper's Gambit: - // quine
                Bookkeeper's Gambit: - // property: page labels
                <[page index, previous scroll]>
            }
            Flock's Disintegration
            Flock's Disintegration

            // if we just started holding telepathy, change the previous scroll to 0
            Telepathic Reflection
            Numerical Reflection: 0
            Equality Distillation
            Numerical Reflection: 0
            Rotation Gambit
            Augur's Exaltation

            // delta scroll (+ve down)
            Rolling Reflection
            Undertaker's Gambit
            Subtractive Distillation

            // check if delta is non-zero...
            Gemini Decomposition
            Numerical Reflection: 0
            Inequality Distillation

            // and if we're holding telepathy
            Telepathic Reflection
            Numerical Reflection: 0
            Maximus Distillation II
            Conjunction Distillation

            {
                // if not scrolling
                // drop delta and property, but keep the new scroll value
                Bookkeeper's Gambit: v--v

                // if scrolling
                // index += delta
                Rotation Gambit
                Additive Distillation

                // index = max(index, 0)
                Numerical Reflection: 0
                Dioscuri Gambit
                Maximus Distillation
                Rotation Gambit II
                Augur's Exaltation

                // read labels
                Rotation Gambit
                Observation Purification

                // index = min(index, len(labels) - 1)
                Undertaker's Gambit
                Length Purification
                Numerical Reflection: 1
                Subtractive Distillation

                Dioscuri Gambit
                Minimus Distillation
                Rotation Gambit II
                Augur's Exaltation

                // get and display new label
                Undertaker's Gambit
                Selection Distillation

                Prospector's Gambit
                Numerical Reflection: 1 // display pages from 1 instead of 0
                Additive Distillation

                {
                    <text: ")">
                    <text: " (">
                }
                Flock's Disintegration

                Rotation Gambit II
                Numerical Reflection: 4
                Flock's Gambit
                Collage Purification

                Send Thought

                // put index and delta back into original order
                Jester's Gambit
            }
            Speaker's Decomposition
            Augur's Exaltation
            Hermes' Gambit

            // insert the index into the label, to send it to the CAD
            Prospector's Gambit
            Reading Purification (text)
            {
                <text: "Songbook: ">
            }
            Flock's Disintegration
            Jester's Gambit
            Additive Distillation

            Numerical Reflection: -3
            Fisherman's Gambit

            // insert new index and delta into quine
            Numerical Reflection: 2
            Flock's Gambit
            Numerical Reflection: 3
            Jester's Gambit
            Surgeon's Exaltation

            // quineify the quine and enqueue the next cast
            Numerical Reflection: 1
            Prospector's Gambit
            Surgeon's Exaltation

            Numerical Reflection: 1
            Rotation Gambit
            Enqueue
        Consideration: }
        Numerical Reflection: 1
        Prospector's Gambit
        Surgeon's Exaltation

        // check if the cassette is running
        {
            {
                Gemini Decomposition
                Numerical Reflection: 0

                // min(len(label), 10)
                Prospector's Gambit
                Length Purification
                Numerical Reflection: 10
                
                Dioscuri Gambit
                Minimus Distillation
                Rotation Gambit II
                Augur's Exaltation
                
                // check label prefix
                Selection Exaltation
                {
                    <text: "Songbook: ">
                }
                Flock's Disintegration
                Equality Distillation
                {
                    // cassette is running, jump out
                    Jester's Gambit
                    Hermes' Gambit
                }
                Vacant Reflection
                Augur's Exaltation
                Hermes' Gambit
            }
            Operating Reflection
            Thoth's Gambit
            Bookkeeper's Gambit: vv

            // if we reached this point, the cassette isn't running
            // if we're looking at an entity, try to recharge with it
            // otherwise, start the cassette
            Mind's Reflection
            Compass' Purification
            Mind's Reflection
            Alidade's Purification
            Scout's Distillation
            
            Undertaker's Gambit
            Augur's Purification
            {
                Jester's Gambit
                Recharge Item
            }
            Rotation Gambit
            Augur's Exaltation
            Hermes' Gambit

            // don't continue out of iris
            Janus' Gambit
        }
        Iris' Gambit

        // parse the index in the label
        Numerical Reflection: 10
        Prospector's Gambit
        Length Purification
        Selection Exaltation
        Scrivener's Purification
        Input Purification

        // extract the page labels prop from the quine
        Jester's Gambit
        Numerical Reflection: 2
        Selection Distillation

        // fish up the pages prop
        Rotation Gambit

        // check for page editing
        // if we're sneaking and holding telepathy, pop the top of the stack, then:
        //   scribe's reflection: read the current page, and push it to the stack
        //   scribe's gambit:     pop the top of the stack, and write it to the current page
        //   name item:           pop the top of the stack, and write it to the label of the current page
        //   garbage:             execute the current page
        //   any other iota:      push the iota back to the stack, then execute the current page
        Stealthy Reflection
        Numerical Reflection: 0
        Maximus Distillation II
        Telepathic Reflection
        Numerical Reflection: 0
        Maximus Distillation II
        Conjunction Distillation
        Consideration: {
            // if not editing
            // drop the page labels prop
            Bookkeeper's Gambit: v-

            // if editing
            // peek the top of the stack
            {
                <Garbage>
                Scribe's Reflection
                Scribe's Gambit
                Name Item
            }
            Grokking Reflection
            Huginn's Gambit // in case the iota is very large, don't have multiple copies on the stack at once
            Muninn's Reflection
            Locator's Distillation
            Numerical Reflection: 1
            Additive Distillation

            {
                // any other iota
                // push the iota back to stack, then execute the current page
                Bookkeeper's Gambit: v-
                Muninn's Reflection
                Grokking Gambit
            }
            {
                // garbage
                // just execute the current page
                Bookkeeper's Gambit: v-
            }
            {
                // scribe's reflection
                // read and push the current page
                Bookkeeper's Gambit: v-

                Observation Purification
                Jester's Gambit
                Selection Distillation
                Grokking Gambit

                Hallucinate Click
                Janus' Gambit
            }
            {
                // scribe's gambit
                // pop and write the current page

                // read pages
                Gemini Decomposition
                Gemini Decomposition
                Observation Purification

                // write current page
                Numerical Reflection: 4
                Fisherman's Gambit II
                Grokking Reflection
                Surgeon's Exaltation
                Schrödinger's Gambit

                // if the page label is null, change it to "Untitled" to show that the page has something in it
                Rotation Gambit II
                Dioscuri Gambit
                Gemini Decomposition
                Observation Purification
                Rotation Gambit
                
                Dioscuri Gambit
                Selection Distillation
                Nullary Reflection
                Equality Distillation
                {
                    // if not null
                    Bookkeeper's Gambit: vvv

                    // if null
                    {
                        <text: "Untitled">
                    }
                    Flock's Disintegration
                    Surgeon's Exaltation
                    Schrödinger's Gambit
                }
                Speaker's Decomposition
                Augur's Exaltation
                Hermes' Gambit

                // if we wrote to the last page, insert another one
                Rotation Gambit II
                Prospector's Gambit
                Observation Purification
                Length Purification
                Numerical Reflection: 1
                Subtractive Distillation
                Equality Distillation
                {
                    // pages
                    Gemini Decomposition
                    Observation Purification
                    Nullary Reflection
                    Integration Distillation
                    Schrödinger's Gambit

                    // page labels
                    Gemini Decomposition
                    Observation Purification
                    Nullary Reflection
                    Integration Distillation
                    Schrödinger's Gambit
                }
                Vacant Reflection
                Augur's Exaltation
                Hermes' Gambit

                Hallucinate Click
                Janus' Gambit
            }
            {
                // name item
                // pop and write the current page label
                Bookkeeper's Gambit: v

                Gemini Decomposition
                Observation Purification
                Rotation Gambit
                Grokking Reflection
                Surgeon's Exaltation
                Schrödinger's Gambit

                Hallucinate Click
                Janus' Gambit
            }
            Numerical Reflection: 5
            Flock's Gambit
            Jester's Gambit
            Selection Distillation
            Hermes' Gambit
        Consideration: }
        Speaker's Decomposition
        Augur's Exaltation
        Hermes' Gambit

        // execute the selected page
        Observation Purification
        Jester's Gambit
        Selection Distillation
        Hermes' Gambit
    Consideration: }

    // insert pages prop
    Numerical Reflection: 1
    Rotation Gambit
    Surgeon's Exaltation

    // insert page labels prop
    Numerical Reflection: 7
    Rotation Gambit
    Surgeon's Exaltation
}

#define Program Distillation [hexcassettes:inspect] = player, text -> boolean
#define Operating Reflection [hexcassettes:busy] = -> list of text iotas
